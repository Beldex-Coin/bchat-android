apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'witness'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlinx-serialization'
apply plugin: 'dagger.hilt.android.plugin'
apply from: 'buildGradle/dependencies.gradle'

configurations.all {
    exclude module: "commons-logging"
}
def canonicalVersionCode = 54
def canonicalVersionName = "2.5.2"

//First Release Date 17-06-2022
//Version Code 15 Version Name 1.0.0

//Second Release Date 19-06-2022
//Version Code 16 Version Name 1.0.1

//Third Release Date 20-06-2022
//Version Code 17 Version Name 1.0.2

//Fourth Release Date 28-06-2022
//Version Code 18 Version Name 1.0.3

//Fifth Release Date 04-07-2022
//Version Code 19 Version Name 1.0.4

//Six Internal Test Release Date 26-07-2022
//Version Code 20 Version Name 1.1.0

//Seventh Internal Test Release Date 27-07-2022
//Version Code 21 Version Name 1.1.1

//Eighth Release Date 26-09-2022
//Version Code 22 Version Name 1.2.0

//Ninth Release Date 05-10-2022
//Version Code 23 Version Name 1.2.1

//Tenth Internal Test Release Date 28-12-2022
//Version Code 24 Version Name 1.2.2

//Eleventh Internal Test Release Date 29-12-2022
//Version Code 25 Version Name 1.2.3

//Twelveth Internal Test Release Date 04-01-2023
//Version Code 26 Version Name 1.2.4

//Thirteenth Internal Test Release Date 04-01-2023
//Version Code 27 Version Name 1.2.5

//Fourteenth Internal Test Release Date 16-01-2023
//Version Code 28 Version Name 2.0.0

//Fifteenth Release Date 06-02-2023
//Version Code 29 Version Name 2.0.1

//Sixteenth Release Date 09-02-2023
//Version Code 30 Version Name 2.0.2

//Seventeenth Release Date 27-02-2023
//Version Code 31 Version Name 2.0.3

//Eighteenth Release Date 31-03-2023
//Version Code 32 Version Name 2.0.4

//Nineteenth Release Data 06-07-2023
//Version Code 33 Version Name 2.1.0

//Twentieth Release Date 29-08-2023
//Version Code 34 Version Name 2.1.1

//Twenty-first Release Date 15-11-2023
//Version Code 35 Version Name 2.2.0

//Twenty-second Release Data 21-12-2023
//Version code 36 Version Name 2.2.1

//Twenty-third Release Data 03-01-2024
//Version code 37 Version Name 2.2.2

//Twenty-fourth Internal Test Release Data 29-01-2024
//Version code 38 Version Name 2.2.3

//Twenty-fifth Release Data 30-01-2024 (Rejected)
//Version code 39 Version Name 2.3.0

//Twenty-sixth Release Data 31-01-2024
//Version code 40 Version Name 2.3.0

//Twenty-seventh Release Data 22-02-2024
//Version code 41 Version Name 2.4.0

//Twenty-eight Internal Test Release Data 05-03-2024
//Version code 42 Version Name 2.4.1

//Twenty-ninth Test Release Data 22-03-2024
//Version code 43 Version Name 2.5.0

//30th  Release Data 22-03-2024
//Version code 44 Version Name 2.5.0

//31th Internal Test Release Data 22-03-2024
//Version code 45 Version Name 2.5.0

//32 Internal Test Release Data 27-05-2024
//Version code 46 Version Name 2.5.1

//33 Internal Test Release Date 09-08-2024
//Version code 54 Version Name 2.5.2

def postFixSize = 10
def abiPostFix = ['armeabi-v7a' : 1,
                  'arm64-v8a'   : 2,
                  'x86_64'      : 4,
                  'universal'   : 5]

android {
    useLibrary 'org.apache.http.legacy'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += [
                "-Xjvm-default=all",
        ]
    }
    packagingOptions {
        resources {
            excludes += ['LICENSE.txt', 'LICENSE', 'NOTICE', 'asm-license.txt', 'META-INF/LICENSE', 'META-INF/NOTICE', 'META-INF/proguard/androidx-annotations.pro']
        }
    }


    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            universalApk true
        }
    }

    defaultConfig {
        versionCode canonicalVersionCode
        versionName canonicalVersionName

        minSdkVersion androidMinimumSdkVersion
        compileSdk androidCompileSdkVersion
        targetSdkVersion androidTargetSdkVersion
        buildToolsVersion '34.0.0'

        multiDexEnabled = true

        vectorDrawables.useSupportLibrary = true
        project.ext.set("archivesBaseName", "bchat")

        buildConfigField "long", "BUILD_TIMESTAMP", getLastCommitTimestamp() + "L"
        buildConfigField "String", "CONTENT_PROXY_HOST", "\"contentproxy.signal.org\""
        buildConfigField "int", "CONTENT_PROXY_PORT", "443"
        buildConfigField "String", "USER_AGENT", "\"OWA\""
        buildConfigField "String[]", "LANGUAGES", "new String[]{\"" + autoResConfig().collect { s -> s.replace('-r', '_') }.join('", "') + '"}'
        buildConfigField "int", "CANONICAL_VERSION_CODE", "$canonicalVersionCode"
        resConfigs autoResConfig()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
        testOptions {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
        externalNativeBuild {
            cmake {
                cppFlags '-std=c++17'
            }
        }
        applicationId 'io.beldex.bchat'
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
        /*main{
            assets.srcDirs = ['src/main/assets', 'src/main/assets/']
        }*/
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
        }
    }

    flavorDimensions = ["distribution", "targetNet"]
    productFlavors {
        play {
            dimension "distribution"

            ext.websiteUpdateUrl = "null"
            buildConfigField "boolean", "PLAY_STORE_DISABLED", "false"
            buildConfigField "String", "NOPLAY_UPDATE_URL", "$ext.websiteUpdateUrl"
        }
        website {
            dimension "distribution"

            ext.websiteUpdateUrl = "https://github.com/Beldex-Coin/bchat-android/releases"
            buildConfigField "boolean", "PLAY_STORE_DISABLED", "true"
            buildConfigField "String", "NOPLAY_UPDATE_URL", "\"$ext.websiteUpdateUrl\""
        }
        mainNet {
            dimension "targetNet"

            buildConfigField "String", "NETWORK_TYPE", "\"mainnet\""
            buildConfigField "String", "REPORT_ISSUE_ID", "\"bdb890a974a25ef50c64cc4e3270c4c49c7096c433b8eecaf011c1ad000e426813\""
            buildConfigField "boolean", "USE_TESTNET", "false"
            buildConfigField "String", "EXPLORER_URL", "\"https://explorer.beldex.io\""
        }
        devNet {
            dimension "targetNet"

            buildConfigField "String", "NETWORK_TYPE", "\"devnet\""
            buildConfigField "String", "REPORT_ISSUE_ID", "\"bd21c8c3179975fa082f221323ae47d44bf38b8f6e39f530c2d07ce7ad4892682d\""
            buildConfigField "boolean", "USE_TESTNET", "true"
            buildConfigField "String", "EXPLORER_URL", "\"http://154.26.139.105\""
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abiName = output.getFilter("ABI") ?: 'universal'
            def postFix = abiPostFix.get(abiName, 0)

            if (postFix >= postFixSize) throw new AssertionError("postFix is too large")
            output.outputFileName = output.outputFileName = "Bchat-${variant.versionName}-${abiName}.apk"
            output.versionCodeOverride = canonicalVersionCode
        }
    }


    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.1"
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
            version '3.10.2'
        }
    }
    ndkVersion '21.4.7075529'
    lint {
        abortOnError true
        baseline file('lint-baseline.xml')
    }
    namespace 'io.beldex.bchat'
    buildToolsVersion '34.0.0'
    compileSdk 34
}

static def getLastCommitTimestamp() {
    new ByteArrayOutputStream().withStream { os ->
        return os.toString() + "000"
    }
}

/**
 * Discovers supported languages listed as under the res/values- directory.
 */
def autoResConfig() {
    def files = new ArrayList<String>()
    def root = file("src/main/res")
    root.eachFile { f -> files.add(f.name) }
    ['en'] + files.collect { f -> f =~ /^values-([a-z]{2}(-r[A-Z]{2})?)$/ }
            .findAll { matcher -> matcher.find() }
            .collect { matcher -> matcher.group(1) }
            .sort()
}